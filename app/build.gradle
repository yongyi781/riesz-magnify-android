apply plugin: 'com.android.model.application'

model {
    repositories {
        libs(PrebuiltLibraries) {
            magnify {
                binaries.withType(StaticLibraryBinary) {
                    staticLibraryFile = file("src/main/halide/${targetPlatform.getName()}/magnify.o")
                }
            }
            copy {
                binaries.withType(StaticLibraryBinary) {
                    staticLibraryFile = file("src/main/halide/${targetPlatform.getName()}/copy.o")
                }
            }
        }
    }

    android {
        compileSdkVersion = 'Google Apis:Google Apis:23'
        buildToolsVersion = "23.0.2"

        defaultConfig.with {
            applicationId = "com.yongyi.rieszmagnifyandroid"
            minSdkVersion.apiLevel = 21
            targetSdkVersion.apiLevel = 23
            versionCode = 1
            versionName = "1.0"
        }

        ndk {
            moduleName = "native"
            cppFlags.addAll([
                    "-fexceptions",
                    "-I${file("../../../Halide/tools")}".toString(),
                    "-I${file("../../../Halide/include")}".toString()
            ])
            ldLibs.addAll(["log", "android"])
            stl = "c++_static"
            abiFilters.addAll(["arm64-v8a"])
        }

        sources {
            main {
                jni {
                    dependencies {
                        library "magnify" linkage "static"
                        library "copy" linkage "static"
                    }
                }
            }
        }

        buildTypes {
            release {
                minifyEnabled = false
                proguardFiles.add(file('proguard-rules.pro'))
            }
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:support-v13:23.1.1'
    compile 'com.android.support:support-v4:23.1.1'
}
